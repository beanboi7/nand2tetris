// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
   // Put you code here:
   
   //computes for zx wire alt method
   //Not16(in=x, out=notx);
   //And16(a=x, b=notx, out=zxi);
   //Mux16(a=x, b=zxi, sel=zx, out=zxoutput);

   //zx can also be done in one line://zy can also be done as zx alt
   Mux16(a=x, b=false, sel=zx, out=zxoutput);
   Mux16(a=y, b=false, sel=zy, out=zyoutput);

   //computes for nx wire
   Not16(in=zxoutput, out=notzxo);
   Mux16(a=zxoutput, b=notzxo, sel=nx, out=nxoutput);
   //computes for ny wire
   Not16(in=zyoutput, out=notzyo);
   Mux16(a=zyoutput, b=notzyo, sel=ny, out=nyoutput);

   //computes for f wire
   Add16(a=nxoutput, b=nyoutput, out=addout);
   And16(a=nxoutput, b=nyoutput, out=andout);
   Mux16(a=andout, b=addout, sel=f, out=fout);
    //computes for no wire
   Not16(in=fout, out=nfout);
   //Mux16(a=fout,b=nfout,sel=no,out=out); //Add this line instead for ALU-nostat.hdl and comment out the below code
   Mux16(a=fout,b=nfout,sel=no,out=out,out[0..7]=zr1,out[8..15]=zr2,out[15]=ng);

   //ng computed here with msb beacuse if output < 0, msb is 1; else msb is 0 meaning positive; thus mux the MSB;
   
   // 1 if (out==0), 0 otherwise
   Or8Way(in=zr1,out=or1);
   Or8Way(in=zr2,out=or2);
   Or(a=or1,b=or2,out=or3);
   Not(in=or3,out=zr);


}